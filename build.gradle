buildscript {
    repositories {
        maven {
            url 'https://artifactory.nordstrom.com/artifactory/gradle'
            credentials {
                username = "${artifactory_user}"
                password = "${artifactory_api_key}"
            }
        }
        maven {
            url 'https://artifactory.nordstrom.com/artifactory/maven'
            credentials {
                username = "${artifactory_user}"
                password = "${artifactory_api_key}"
            }
        }
    }
    dependencies {
        classpath 'com.google.guava:guava:28.1-jre'
        classpath 'org.jfrog.buildinfo:build-info-extractor-gradle:4.9.0'
        classpath 'io.spring.gradle:dependency-management-plugin:1.0.3.RELEASE'
        classpath 'com.github.jengelman.gradle.plugins:shadow:4.0.4'
        classpath "com.commercehub.gradle.plugin:gradle-avro-plugin:0.17.0"
    }
}

plugins {
    id 'application'
    id "maven-publish"
    id "com.jfrog.artifactory" version "4.13.0"
    id 'com.github.spotbugs' version '2.0.0' apply false
    id 'net.nemerosa.versioning' version '2.8.2'
    id "com.github.imflog.kafka-schema-registry-gradle-plugin" version "0.7.0"
}
application {
    mainClassName = 'com.nordstrom.nap.event.App'
}

ext {
    //https://proton.vip.nordstrom.com/docs/faq.html#what-version-of-kafka-do-you-run
    confluentVersion = '5.4.1'
    kafkaVersion = '2.4.0'
    kstreamsVersion = '2.4.0'

    //NAP dependencies
    eventSdkVersion = '0.8.2'
    scaffoldVersion = 'HEAD-9ba343c'

    //AWS dependencies
    awsEncryptionSdkVersion = '1.3.6'
    awsSdk2BomVersion = '2.3.7'
    awsSdkBomVersion = '1.11.754'
    dynamoDBClientLegacyVersion = '1.11.489'

    //Metrics
    dogstatsdVersion = '2.7'
    newrelicVersion = '5.1.0'

    //Coding standards
    annotationsVersion = '18.0.0'
    checkstyleVersion = '8.24'
    jacocoVersion = "0.8.3"
    spotbugsVersion = '4.0.1'

    avroVersion = '1.9.1'
    commonsIoVersion = '2.6'
    commonsLangVersion = '3.9'
    gsonVersion = '2.8.5'
    guavaVersion = '28.1-jre'
    jacksonVersion = '1.9.13'
    jodaTimeVersion = '2.10.1'
    junitVersion = '5.3.2'
    jxpathVersion = '1.3'
    log4jVersion = '2.11.1'
    mockitoVersion = '3.2.4'
    picoCliVersion = '3.9.0'
    snappyVersion = '1.1.7.2'
}

apply plugin: 'checkstyle'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'com.github.spotbugs'
apply plugin: 'com.jfrog.artifactory'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'jacoco'
apply plugin: 'java'
apply plugin: 'maven-publish'
apply plugin: "com.commercehub.gradle.plugin.avro-base"
apply plugin: "com.commercehub.gradle.plugin.avro"
apply plugin: "com.github.imflog.kafka-schema-registry-gradle-plugin"

group 'com.mahen.poc'
version '1.0-SNAPSHOT'

sourceCompatibility = JavaVersion.VERSION_11

dependencies {
    //NAP
    implementation("com.nordstrom.nap.ingestion:scaffold:$scaffoldVersion") {
        exclude group: 'com.nordstrom.nap.ingestion', module: 'schemas'
        exclude group: 'com.nordstrom.nap', module: 'schemas'
        exclude group: 'com.nordstrom.nap', module: 'core'
    }
    implementation("com.nordstrom.events:sdk:$eventSdkVersion") {
        exclude group: 'com.nordstrom.event.common', module: 'schema-repository'
    }

    //AWS
    implementation "com.amazonaws:aws-encryption-sdk-java:$awsEncryptionSdkVersion"
    implementation "com.amazonaws:aws-java-sdk-dynamodb:$awsSdkBomVersion"
    implementation "com.amazonaws:aws-java-sdk-kms:$awsSdkBomVersion"
    implementation "org.apache.logging.log4j:log4j-api:$log4jVersion"
    implementation "org.apache.logging.log4j:log4j-core:$log4jVersion"
    implementation "org.apache.logging.log4j:log4j-slf4j-impl:$log4jVersion"

    implementation "com.datadoghq:java-dogstatsd-client:$dogstatsdVersion"
    implementation "com.google.guava:guava:$guavaVersion"
    implementation "com.newrelic.agent.java:newrelic-api:$newrelicVersion"
    implementation "commons-io:commons-io:$commonsIoVersion"
    implementation "info.picocli:picocli:$picoCliVersion"

    //Kafka
    implementation "io.confluent:kafka-avro-serializer:$confluentVersion"
    implementation "io.confluent:kafka-streams-avro-serde:$confluentVersion"
    implementation "org.apache.kafka:kafka-clients:$kstreamsVersion"
    implementation "org.apache.kafka:kafka-streams-test-utils:$kstreamsVersion"
    implementation "org.apache.kafka:kafka-streams:$kstreamsVersion"
    implementation "org.xerial.snappy:snappy-java:$snappyVersion"

    compileOnly "com.github.spotbugs:spotbugs-annotations:$spotbugsVersion"
    compileOnly "org.jetbrains:annotations:$annotationsVersion"

    testImplementation "org.junit.jupiter:junit-jupiter-api:$junitVersion"
    testImplementation "org.mockito:mockito-core:$mockitoVersion"
    testImplementation "org.mockito:mockito-junit-jupiter:$mockitoVersion"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junitVersion"
}

repositories {
    maven {
        url = uri("https://artifactory.nordstrom.com/artifactory/gradle")
        credentials {
            username = "${artifactory_user}"
            password = "${artifactory_api_key}"
        }
    }
    maven {
        url = uri("https://artifactory.nordstrom.com/artifactory/maven")
        credentials {
            username = "${artifactory_user}"
            password = "${artifactory_api_key}"
        }
    }
}

checkstyle {
    toolVersion = "$checkstyleVersion"
    ignoreFailures = false
    showViolations = true
    maxWarnings = 0
    configProperties = ["suppressionFile": project(':').file('config/checkstyle/suppressions.xml')]
}

tasks.withType(com.github.spotbugs.SpotBugsTask) {
    reports.xml.enabled = false
    reports.html.enabled = true
    ignoreFailures = false
}

tasks.withType(JavaCompile) {
    options.compilerArgs << '-Xlint:unchecked'
    options.deprecation = true
}

javadoc {
    source = sourceSets.main.allJava
    options.addBooleanOption('html5', true)
}

jacoco {
    toolVersion = "$jacocoVersion"
}

jacocoTestReport {
    reports.xml.enabled = false
    reports.csv.enabled = false
    reports.html.enabled = true
}

test.finalizedBy jacocoTestReport

versionFile {
    file = new File("$buildDir/generated-resources", "version.properties")
}

sourceSets {
    main {
        output.dir("$buildDir/generated-resources/version.properties", builtBy: versionFile)
    }
}

test {
    useJUnitPlatform()
    testLogging {
        exceptionFormat "full"
        showExceptions = true
        showStackTraces = true
        showStandardStreams = true
    }
}

jar {
    from {
        configurations.compileClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    exclude 'META-INF/*.RSA', 'META-INF/*.SF', 'META-INF/*.DSA'
}