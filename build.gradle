import com.commercehub.gradle.plugin.avro.GenerateAvroProtocolTask
import com.commercehub.gradle.plugin.avro.GenerateAvroSchemaTask

buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        maven {
            url = 'https://packages.confluent.io/maven/'
        }
        maven {
            url = 'https://repo.maven.apache.org/maven2'
        }
    }
    dependencies {
        classpath 'com.google.guava:guava:28.1-jre'
        classpath 'org.jfrog.buildinfo:build-info-extractor-gradle:4.9.0'
        classpath 'io.spring.gradle:dependency-management-plugin:1.0.3.RELEASE'
        classpath 'com.github.jengelman.gradle.plugins:shadow:4.0.4'
        classpath "com.commercehub.gradle.plugin:gradle-avro-plugin:0.17.0"
        classpath "com.github.imflog:kafka-schema-registry-gradle-plugin:0.9.0"
    }
}

plugins {
    id 'application'
    id "maven-publish"
    id "com.jfrog.artifactory" version "4.13.0"
    id 'com.github.spotbugs' version '2.0.0' apply false
    id 'net.nemerosa.versioning' version '2.8.2'
    id "com.github.imflog.kafka-schema-registry-gradle-plugin" version "0.7.0"
}
application {
    mainClassName = 'com.nordstrom.nap.event.App'
}

ext {
    //Kafka Version
    confluentVersion = '5.4.2'
    kafkaVersion = '2.5.0'
    kstreamsVersion = '2.5.0'

    //Coding standards
    annotationsVersion = '18.0.0'
    checkstyleVersion = '8.24'
    jacocoVersion = "0.8.3"
    spotbugsVersion = '4.0.1'

    avroVersion = '1.9.1'
    commonsIoVersion = '2.6'
    commonsLangVersion = '3.9'
    gsonVersion = '2.8.5'
    guavaVersion = '28.1-jre'
    jacksonVersion = '1.9.13'
    jodaTimeVersion = '2.10.1'
    junitVersion = '5.3.2'
    jxpathVersion = '1.3'
    log4jVersion = '2.11.1'
    mockitoVersion = '3.2.4'
    picoCliVersion = '3.9.0'
    snappyVersion = '1.1.7.2'
}

apply plugin: 'checkstyle'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'com.github.spotbugs'
apply plugin: 'com.jfrog.artifactory'
apply plugin: 'jacoco'
apply plugin: 'java-library'
apply plugin: 'maven-publish'
apply plugin: "com.commercehub.gradle.plugin.avro-base"
apply plugin: "com.commercehub.gradle.plugin.avro"
apply plugin: "com.github.imflog.kafka-schema-registry-gradle-plugin"

group 'com.mahendran.kafka'
version = versioning.info.tag

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

dependencies {
    implementation "com.google.guava:guava:$guavaVersion"
    implementation "commons-io:commons-io:$commonsIoVersion"
    //Kafka
    implementation "io.confluent:kafka-avro-serializer:$confluentVersion"
    implementation "io.confluent:kafka-streams-avro-serde:$confluentVersion"
    implementation "org.apache.kafka:kafka-clients:$kstreamsVersion"
    implementation "org.apache.kafka:kafka-streams-test-utils:$kstreamsVersion"
    implementation "org.apache.kafka:kafka-streams:$kstreamsVersion"
    implementation "org.xerial.snappy:snappy-java:$snappyVersion"

    compileOnly "com.github.spotbugs:spotbugs-annotations:$spotbugsVersion"
    compileOnly "org.jetbrains:annotations:$annotationsVersion"

    testImplementation "org.junit.jupiter:junit-jupiter-api:$junitVersion"
    testImplementation "org.mockito:mockito-core:$mockitoVersion"
    testImplementation "org.mockito:mockito-junit-jupiter:$mockitoVersion"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junitVersion"
}

checkstyle {
    toolVersion = "$checkstyleVersion"
    ignoreFailures = false
    showViolations = true
    maxWarnings = 0
    configProperties = ["suppressionFile": project(':').file('config/checkstyle/suppressions.xml')]
}


tasks.withType(com.github.spotbugs.SpotBugsTask) {
    reports.xml.enabled = false
    reports.html.enabled = true
    ignoreFailures = false
    excludeFilter = file("$project.rootDir/spotbugs_exclude.xml")
}

tasks.withType(JavaCompile) {
    options.compilerArgs << '-Xlint:unchecked'
    options.deprecation = true
}

javadoc {
    source = sourceSets.main.allJava
    options.addBooleanOption('html5', true)
}

jacoco {
    toolVersion = "$jacocoVersion"
}

jacocoTestReport {
    reports.xml.enabled = false
    reports.csv.enabled = false
    reports.html.enabled = true
}

test.finalizedBy jacocoTestReport

test {
    useJUnitPlatform()
}

javadoc {
    source = sourceSets.main.allJava
}


jar {
    from {
        configurations.compileClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    exclude 'META-INF/*.RSA', 'META-INF/*.SF', 'META-INF/*.DSA'
}

def allAvprs = "$buildDir/generated-main-avro-avpr"
def allAvscs = "$buildDir/generated-main-avro-avsc"

task copySchemas(type: Copy) {
    from "src/main/avro"
    include("**/*.avsc")
    into allAvscs
}
task("generateProtocol", type: GenerateAvroProtocolTask) {
    source file("src/main/avro")
    include("**/*.avdl")
    outputDir = file(allAvprs)
}

task("generateSchema", type: GenerateAvroSchemaTask) {
    dependsOn generateProtocol
    source file("src/main/avro")
    source file(allAvprs)
    include("**/*.avpr")
    outputDir = file(allAvscs)
}

jar {
    from('build/generated-main-avro-avsc/') {
        include '**/*.avsc'
        into 'avro'
    }
    dependsOn generateSchema
}
repositories {
    mavenCentral()
    maven {
        url "https://packages.confluent.io/maven/"
    }
    maven {
        url = uri('https://artifactory.nordstrom.com/artifactory/gradle')
        credentials {
            username = "${artifactory_user}"
            password = "${artifactory_api_key}"
        }
    }
    maven {
        url = uri('https://artifactory.nordstrom.com/artifactory/maven')
        credentials {
            username = "${artifactory_user}"
            password = "${artifactory_api_key}"
        }
    }
}

//repositories {
//    maven {
//        url "https://artifactory.nordstrom.com/artifactory/maven"
//        credentials {
//            username = "${artifactory_user}"
//            password = "${artifactory_api_key}"
//        }
//    }
//}


//publishing {
//    publications {
//        mavenJava(MavenPublication) {
//            from components.java
//        }
//    }
//}
publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            pom {
                name = "Nordstrom 1-Hop Event Merger Repository"
                description = "Collection of business event schemas."
                url = "https://gitlab.nordstrom.com/1-hop/event-merger"

                developers {
                    developer {
                        id = "nordstrom"
                        name = "Nordstrom developers"
                    }
                }
                scm {
                    connection = "https://gitlab.nordstrom.com/1-hop/event-merger"
                    developerConnection = "https://gitlab.nordstrom.com/1-hop/event-merger"
                    url = "https://gitlab.nordstrom.com/1-hop/event-merger"
                }
            }
        }
    }
}

artifactory {
    contextUrl = "${artifactory_contextUrl}"
    publish {
        repository {
            repoKey = "gradle-local"
            username = "${artifactory_user}"
            password = "${artifactory_api_key}"
            maven = true
        }
        defaults {
            publications("mavenJava")
            publishArtifacts = true
        }
    }
    resolve {
        repository {
            repoKey = "gradle"
            username = "${artifactory_user}"
            password = "${artifactory_api_key}"
            // Nordstrom Artifactory team asks to use API key in place of password.
            maven = true
        }
    }
}